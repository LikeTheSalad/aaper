plugins {
    id "java-gradle-plugin"
    id "com.likethesalad.tools.java-library"
    id "com.likethesalad.tools.android-compilation-testing"
    alias(libs.plugins.buildConfig)
}

configurations {
    testLocalDependency {
        canBeResolved(false)
        canBeConsumed(false)
    }
    testLocalClasspath {
        canBeConsumed(false)
        canBeResolved(true)
        extendsFrom(testLocalDependency)
    }
}

libConventions {
    setJavaVersion("11")
}

dependencies {
    implementation project(":aaper-api")
    implementation libs.bundles.asm
    implementation libs.classGraph
    compileOnly libs.android.plugin
    testPluginDependency libs.android.plugin
    testLocalDependency project(":aaper-api")
}

tasks.withType(Test) {
    systemProperty "aaper_local_dependencies", configurations.testLocalClasspath.files.join(",")
}

buildConfig {
    packageName("${group}.generated")
    buildConfigField("String", "SDK_DEPENDENCY_URI", "\"$group:aaper:$version\"")
}

gradlePlugin {
    plugins {
        aaperPlugin {
            id = 'com.likethesalad.aaper'
            implementationClass = 'com.likethesalad.android.aaper.plugin.AaperPlugin'
        }
    }
}
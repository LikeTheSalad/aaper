plugins {
    id "java-gradle-plugin"
    id 'org.jetbrains.kotlin.jvm'
    id 'com.likethesalad.testing.android.compilation'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

configurations {
    testLocalDependency {
        canBeResolved(false)
        canBeConsumed(false)
    }
    testLocalClasspath {
        canBeConsumed(false)
        canBeResolved(true)
        extendsFrom(testLocalDependency)
    }
}

dependencies {
    implementation project(":aaper-api")
    implementation "org.ow2.asm:asm:9.6"
    implementation "org.ow2.asm:asm-util:9.6"
    implementation "io.github.classgraph:classgraph:4.8.165"
    compileOnly "com.android.tools.build:gradle:7.4.0"
    testPluginDependency "com.android.tools.build:gradle:7.4.0"
    testLocalDependency project(":aaper-api")
}

tasks.withType(Test) {
    systemProperty "aaper_local_dependencies", configurations.testLocalClasspath.files.join(",")
}

buildConfig {
    packageName("${group}.generated")
    buildConfigField("String", "SDK_DEPENDENCY_URI", "\"$group:aaper:$version\"")
}

gradlePlugin {
    plugins {
        aaperPlugin {
            id = 'com.likethesalad.aaper'
            implementationClass = 'com.likethesalad.android.aaper.plugin.AaperPlugin'
        }
    }
}